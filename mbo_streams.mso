#This code was printed to .pdf format for submission.
#Copy the text to a .txt file and save as mbo_streams.mso to open at EMSO.

using "types";

Model str3ph
	VARIABLES
	Qf			as 	positive		(Brief="Liquid std. vol. flow rate",Unit='m^3/d',Default=1e4);
	Qg			as 	positive		(Brief="Gas std. vol. flow rate",Unit='m^3/d',Default=2e6);
	BSW			as 	fraction		(Brief="Basic sediments and water",Default=0.5);
	P			as 	pressure		(Brief="Pressure",DisplayUnit='kPa',Default=10);
	T			as 	temperature		(Brief="Temperature",Default=300);
	hO			as 	enth_mass		(Brief="Oil specific enthalpy",Default=250);
	hG			as 	enth_mass		(Brief="Gas specific enthalpy",Default=400);
	hW			as 	enth_mass		(Brief="Water specific enthalpy",Default=250);
	rhoL		as 	dens_mass		(Brief="Liquid phase mass density",Default=900);
	rhoG		as 	dens_mass		(Brief="Gas phase mass density",Default=10);
	rho_Wstd	as 	dens_mass		(Brief="Water phase mass density at std. cond.",Default=1005);
	Rs			as 	positive		(Brief="Solution gas-oil ratio",Unit='m^3/m^3',Default=10);
	Rv			as 	positive		(Brief="Vaporized oil-gas ratio",Unit='l/m^3',Default=0.05);
	Bo			as 	positive		(Brief="Oil phase FVF",Unit='m^3/m^3',Default=1.05);
	Bg			as 	positive		(Brief="Gas phase FVF",Unit='m^3/m^3',Default=0.1);
	Bw			as 	positive		(Brief="Water phase FVF",Unit='m^3/m^3',Default= 0.95);
	d60			as 	positive		(Brief="Oil specific gravity at 60 °F",Lower=1e-6,Default=0.85);
	d60G		as 	positive		(Brief="Hypothetical d60 for gas phase",Lower=1e-6,Default=0.6);
	Tbn			as 	temperature		(Brief="Oil mormal boiling point",Default=450);
	TbnG		as 	temperature		(Brief="Hypothetial Tbn for gas phase",Default=250);
	dg			as 	positive		(Brief="Gas specific gravity",Default=0.7);
	dgL			as 	positive		(Brief="Solution gas specific gravity",Default=0.95);
	Ycg			as 	positive		(Brief="Condensate/gas yield",Unit='l/m^3',Default=4);
	YcgL		as 	positive		(Brief="Solution condensate/gas yield",Unit='l/m^3',Default=5.5);
	ws			as 	fraction		(Brief="Water phase salt weitgh content",Default=0.2);
end

Model null3ph as str3ph
	EQUATIONS
	Qf 		= 0*'m^3/d';
	Qg 		= 0*'m^3/d';
	BSW 		= 0.00;
	ws		= 0.00;
	T 		= 300 * 'K';
	hO 		= 0.00 * 'kJ/kg';
	hG 		= 0.00 * 'kJ/kg';
	hW 		= 0.00 * 'kJ/kg';
	rhoL 	= 800 * 'kg/m^3';
	rhoG 	= 1.00 * 'kg/m^3';
	Rs 		= 0.00 * 'm^3/m^3';
	Rv 		= 0.00 * 'l/m^3';
	Bo 		= 1.00;
	Bg 		= 1.00;
	Bw 		= 1.00;
	rho_Wstd	= 1000 * 'kg/m^3';
	d60 		= 0.85;
	d60G		= d60;
	Tbn 		= 500*'K';
	TbnG		= Tbn;
	dg 		= 1.00;
	dgL		= dg;
	Ycg 		= 1*'l/m^3';
	YcgL		= Ycg;
end

Model triphase_stream as str3ph
	PARAMETERS
	R			as positive		(Brief="Ideal gas constant",Unit='kJ/kmol/K',Default=8.31447);
	Tref			as temperature		(Brief="Reference temperature",Default=288.70556);
	Pref			as pressure		(Brief="Reference pressure",DisplayUnit='kPa',Default=1);
	rho_H2Ostd	as dens_mass		(Brief="Pure water mass density at std. cond.",Default=999.0054);
	rho_AIRstd	as dens_mass		(Brief="Air mass density at std. cond.",Default=1.2938);

	VARIABLES
	hGres		as enth_mass		(Brief="Gas specific residual enthalpy",Default=-60);
	DhLG			as enth_mass		(Brief="Gas liberation specific enthapy",Default=450);
	Zg			as positive		(Brief="Gas compressibility factor",Default=0.7);
	Tpc			as temperature		(Brief="Gas pseudo-critical temperature",Default=350);
	Ppc			as pressure		(Brief="Gas pseudo-critical pressure",DisplayUnit='kPa',Default=30);
	DVWT			as Real			(Brief="Water phase specific volume temperature effect",Default=0);
	DVWP			as Real			(Brief="Water phase specific volume pressure effect",Default=0);
	IDVWT		as Real			(Brief="Integral term of DVWT",Default=30);
	IDVWTSQ		as Real			(Brief="Integral term of the DVWT square",Default=150);

	EQUATIONS
	"Oil specific enthalpy"
	hO/'Btu/lb' = ((T/'degR')-(Tref/'degR'))*(
		(-1.17126+(0.023722+0.024907*d60)*((Tbn/'degR')^(1/3)/d60) 
			+ (1.14982-0.046535*((Tbn/'degR')^(1/3)/d60))/d60)
			+ (1/2)*1e-4*(1+0.82463*((Tbn/'degR')^(1/3)/d60))*(1.12172-0.27634/d60)
				*(707.67^2-(Tref/'degR')^2)/(707.67-(Tref/'degR'))
			- (1/3)*1e-8*(1+0.82463*((Tbn/'degR')^(1/3)/d60))*(2.9027-0.70958/d60)
				*(707.67^3-(Tref/'degR')^3)/(707.67-(Tref/'degR'))
	);

	"Gas specific enthalpy"
	hG - hGres - DhLG = R/(dg*29*'kg/kmol')*(T-Tref)*(
			(0.7457*dg^2 - 1.3405*dg + 2.222) +
			1e-3*(19.894*dg - 1.902)/2*(473.15^2-(Tref/'K')^2)/(473.15-(Tref/'K')) +
			1e-6*(-6.6562*dg + 1.5103)/3*(473.15^3-(Tref/'K')^3)/(473.15-(Tref/'K'))
		);

	"Gas liberation specific enthapy"
	DhLG = R/(dg*29*'kg/kmol')*'degR'*(141.5/d60G-131.5)*(25.724/1.0937);

	"Gas specific residual enthalpy"
	hGres = R/(dg*29*'kg/kmol')*Tpc*(
		(P/Ppc)*((0.083-1.097/(T/Tpc)^1.6) + (0.1432*dg - 0.0669)*(0.139-0.894/(T/Tpc)^4.2)) -
		(Pref/Ppc)*((0.083-1.097/(Tref/Tpc)^1.6) + (0.1432*dg - 0.0669)*(0.139-0.894/(Tref/Tpc)^4.2))
		);

	"Water specific enthalpy"
	hW/'kJ/kg'*'K/degR' = ((T-Tref)/'degR')*(
		(4.18 - ws*(rho_Wstd/'kg/m^3')/(1+DVWP)*(4.396e-3 - 4.8e-6*ws*(rho_Wstd/'kg/m^3')/(1+DVWP)))
		- ws*(rho_Wstd/'kg/m^3')/(1+DVWP)*(4.396e-3 - 2*4.8e-6*ws*(rho_Wstd/'kg/m^3')/(1+DVWP))*IDVWT
		+ 4.8e-6*(ws*(rho_Wstd/'kg/m^3')/(1+DVWP))^2*IDVWTSQ
		);

	"Liquid phase mass density"
	rhoL*((1-BSW)*Bo + BSW*Bw) = (1-BSW)*d60*rho_H2Ostd + BSW*rho_Wstd;

	"Gas phase mass density"
	rhoG * Zg * R * T = P * (dg*29.0*'kg/kmol');

	"Oil phase formation volume factor"
	Bo = 1 + 4.677e-4*(Rs/'ft^3/bbl') 
		+ 1.751e-5*((T/'degR')-(459.67+60))*(141.5/d60-131.5)/
			(dgL*(1+5.912e-5*(141.5/d60-131.5)*(40*1.8+32)*ln(14.7/114.7)))
		- 1.811e-8*(Rs/'ft^3/bbl')*((T/'degR')-(459.67+60))*(141.5/d60-131.5)/
			(dgL*(1+5.912e-5*(141.5/d60-131.5)*(40*1.8+32)*ln(14.7/114.7)));

	"Gas phase formation volume factor"
	Bg = (Pref/P)*(T/Tref)*Zg;

	"Water phase formation volume factor"
	Bw = (1+DVWP)/(1+DVWT);

	"Gas compressibility factor"
	Zg = 1 + (0.3265 - 1.0700/(T/Tpc) - 0.5339/(T/Tpc)^3 + 0.01569/(T/Tpc)^4 
		- 0.05165/(T/Tpc)^5)*(0.27*(P/Ppc)/(Zg*(T/Tpc)))
		+ (0.5474 - 0.7361/(T/Tpc) + 0.1844/(T/Tpc)^2)*(0.27*(P/Ppc)/(Zg*(T/Tpc)))^2 
		- 0.1056*(-0.7361/(T/Tpc) + 0.1844/(T/Tpc)^2)*(0.27*(P/Ppc)/(Zg*(T/Tpc)))^5 
		+ 0.6134*exp(-0.7210*((0.27*(P/Ppc)/(Zg*(T/Tpc))))^2)*
			(1 + 0.7210*(0.27*(P/Ppc)/(Zg*(T/Tpc)))^2)*((0.27*(P/Ppc)/(Zg*(T/Tpc)))^2/(T/Tpc)^3);

	"Gas pseudo-critical temperature and pressure"
	Tpc/'K' = 93.3 + 180.6*dg - 6.94*dg^2;
	Ppc/'bar' = 46.66 + 1.03*dg - 2.58*dg^2;

	"Water phase (brine) mass density at std. cond."
	rho_Wstd/'lb/ft^3' = 62.638 + 0.438603*ws + 1.60074e-3*ws^2;

	"Water (brine) phase specific volume temperature effect"
	DVWT = -1.0001e-2 + 1.33391e-4*(T/'degR'-459.67) + 5.50654e-7*(T/'degR'-459.67)^2;

	"Water (brine) phase specific volume pressure effect"
	DVWP = -(3.58922e-7 + 1.95301e-9*(T/'degR'-459.67))*(P/'psi')
		-(2.25341e-10 + 1.72834e-13*(T/'degR'-459.67))*(P/'psi')^2;

	"Integral term of water phase specific volume temperature effect"
	IDVWT = -1.0001e-2 + 1.33391e-4/2*(707.67^2-(Tref/'degR')^2)/(707.67-(Tref/'degR'))
		+ 5.50654e-7/3*(707.67^3-(Tref/'degR')^3)/(707.67-(Tref/'degR'));
	
	"Integral of IDVWT square"
	IDVWTSQ = 1.0001^2e-4 - 1.33391e-6*(707.67^2-(Tref/'degR')^2)/(707.67-(Tref/'degR'))
		+ 2.25966e-9*(707.67^3-(Tref/'degR')^3)/(707.67-(Tref/'degR'))
		+ 3.67261e-11*(707.67^4-(Tref/'degR')^4)/(707.67-(Tref/'degR'))
		+ 6.06440e-14*(707.67^5-(Tref/'degR')^5)/(707.67-(Tref/'degR'));

	"Hypothetical oil specific gravity at gas phase"
	d60G/d60 = (Pref/P)^0.010*exp(100/450 - 100/(T/'K'));

	"Hypothetical oil normal boiling point as gas phase"
	((TbnG/'degR')^(1/3)/d60G) = ((Tbn/'degR')^(1/3)/d60);

	"Dissolved gas specific gravity"
	dg/dgL = (Pref/P)^0.005*exp(600/450 - 600/(T/'K'));

	"Dissolved condensate/gas yield"
	Ycg/YcgL = (Pref/P)^0.005*exp(600/450 - 600/(T/'K'));
end

Model triphase_source
	PARAMETERS
	R			as positive		(Brief="Ideal gas constant",Unit='kJ/kmol/K',Default=8.31447);
	Tref			as temperature		(Brief="Reference temperature for std. cond.",Default=288.70556);
	Pref			as pressure		(Brief="Reference pressure for std. cond.",DisplayUnit='kPa',Default=1);
	rho_H2Ostd	as dens_mass		(Brief="Pure mass water density at std. cond.",Default=999.054);
	rho_AIRstd	as dens_mass		(Brief="Air mass density at std. cond.",Default=1.2938);

	VARIABLES
out	Outlet	as triphase_stream;

	Qo		as positive			(Brief="Oil std. vol. flow rate",Unit='m^3/d',Default=1e4);
	Qw		as positive			(Brief="Water std. vol. flow rate",Unit='m^3/d',Default=1e4);
	Rgo		as positive			(Brief="Gas/oil ratio",Lower=-1e-4,Unit='m^3/m^3',Default=200);
	API		as positive			(Brief="Oil API",Default=30);
	MMg		as molweight			(Brief="Gas molar mass",Default=20);
	Kw		as positive			(Brief="Oil watson K-factor",Default=11.3);
	Cws		as positive			(Brief="Water phase salt mass concentration",Unit='g/l',Default=200);
	rhoO		as dens_mass			(Brief="Oil phase mass density at process conditions",Default=800);
	rhoW		as dens_mass			(Brief="Water phase mass density at process conditions",Default=1030);

	EQUATIONS
	"Oil vol. flow rate at std. cond."
	Qo = Outlet.Qf*(1-Outlet.BSW);
	
	"Water vol. flow rate at std. cond."
	Qw = Outlet.Qf*Outlet.BSW;
	
	"Gas/oil Ratio"
	Outlet.Qg = Rgo * Qo;

	"Oil API grade"
	API = 141.5/Outlet.d60 - 131.5;

	"Gas molar mass"
	MMg = Outlet.dg * 29*'kg/kmol';

	"Oil watson K-factor"
	Kw * Outlet.d60 = (Outlet.Tbn/'degR')^(1/3);

	"Water phase (brine) salt mass concentration"
	Cws*(1-Outlet.ws) = rho_H2Ostd*Outlet.ws;

	"Relationship between oil formation volume factor and mass denisty"
	rhoO * Outlet.Bo = Outlet.d60 * rho_H2Ostd;

	"Water phase (brine) mass density @T,P"
	rhoW * Outlet.Bw = Outlet.rho_Wstd;

	"Solution gas/oil ratio"
	Outlet.Rs/'ft^3/bbl' = 
		0.0362*(Outlet.dg*(1 + 5.912e-5*(141.5/Outlet.d60-131.5)*(40*1.8+32)*ln(14.7/114.7)))
		*(Outlet.P/'psi')^1.0937*exp(25.724*(141.5/Outlet.d60-131.5)/(Outlet.T/'degR'));

	"Vaporized oil/gas ratio"
	Outlet.Rv/Outlet.Ycg = (4.7e-7*(Outlet.P/'psi')^2 - 1.4e-3*(Outlet.P/'psi') + 2.3)*
		exp(-5.438e-3*Outlet.dg)*exp(-6.8e8*Outlet.d60/(Outlet.T/'degR')^3);
end

Model triphase_sink
	PARAMETERS
	R			as positive		(Brief="Ideal gas constant",Unit='kJ/kmol/K',Default=8.31447);
	Tref			as temperature		(Brief="Reference temperature for std. cond.",Default=288.70556);
	Pref			as pressure		(Brief="Reference pressure for std. cond.",DisplayUnit='kPa',Default=1);
	rho_H2Ostd	as dens_mass		(Brief="Pure water mass density at std. cond.",Default=999.054);
	rho_AIRstd	as dens_mass		(Brief="Air mass density at std. cond.",Default=1.2938);

	VARIABLES
in	Inlet	as str3ph;

	Qo		as positive			(Brief="Water std. vol. flow rate",Unit='m^3/d',Default=1e4);
	Qw		as positive			(Brief="Water std. vol. flow rate",Unit='m^3/d',Default=1e4);
	Rgo		as positive			(Brief="Gas/oil ratio",Lower=-1e-4,Unit='m^3/m^3',Default=200);
	API		as positive			(Brief="Oil API",Default=30);
	MMg		as molweight			(Brief="Gas molar mass",Default=20);
	Kw		as positive			(Brief="Oil watson K-factor",Default=11.3);
	Cws		as positive			(Brief="Water phase salt mass concentration",Unit='g/l',Default=200);
	rhoO		as dens_mass			(Brief="Oil phase mass density at process conditions",Default=800);
	rhoW		as dens_mass			(Brief="Water phase mass density at process conditions",Default=1030);

	EQUATIONS
	"Oil vol. flow rate at std. cond."
	Qo = Inlet.Qf*(1-Inlet.BSW);

	"Water vol. flow rate at std. cond."
	Qw = Inlet.Qf*Inlet.BSW;

	"Gas/oil ratio"
	Inlet.Qg = Rgo * Qo;

	"Oil API grade"
	API = 141.5/Inlet.d60 - 131.5;

	"Gas molar mass"
	MMg = Inlet.dg * 29*'kg/kmol';

	"Oil watson K-factor"
	Kw * Inlet.d60 = (Inlet.Tbn/'degR')^(1/3);

	"Water phase (brine) salt mass concentration"
	Cws*(1-Inlet.ws) = rho_H2Ostd*Inlet.ws;

	"Relationship between Formation oil formation volume factor and mass denisty"
	rhoO * Inlet.Bo = Inlet.d60 * rho_H2Ostd;
	
	"Water phase (brine) mass density @T,P"
	rhoW * Inlet.Bw = Inlet.rho_Wstd;
end

Model triphase_ref
	VARIABLES
in	Inlet	as str3ph;
out	Outlet	as str3ph;

	EQUATIONS
	Outlet.BSW		= Inlet.BSW;
	Outlet.ws			= Inlet.ws;
	Outlet.P			= Inlet.P;
	Outlet.T			= Inlet.T;
	Outlet.hO			= Inlet.hO;
	Outlet.hG			= Inlet.hG;
	Outlet.hW			= Inlet.hW;
	Outlet.rhoL		= Inlet.rhoL;
	Outlet.rhoG		= Inlet.rhoG;
	Outlet.Rs			= Inlet.Rs;
	Outlet.Rv			= Inlet.Rv;
	Outlet.Bo			= Inlet.Bo;
	Outlet.Bg			= Inlet.Bg;
	Outlet.Bw			= Inlet.Bw;
	Outlet.rho_Wstd	= Inlet.rho_Wstd;
	Outlet.d60		= Inlet.d60;
	Outlet.d60G		= Inlet.d60G;
	Outlet.Tbn		= Inlet.Tbn;
	Outlet.TbnG		= Inlet.TbnG;
	Outlet.dg			= Inlet.dg;
	Outlet.dgL		= Inlet.dgL;
	Outlet.Ycg		= Inlet.Ycg;
	Outlet.YcgL		= Inlet.YcgL;
end

Model str2ph as str3ph
	EQUATIONS
	"No water"
	BSW 		= 0.00;
	ws		= 0.00;
	hW 		= 0.00 * 'kJ/kg';
	Bw 		= 1.00;
	rho_Wstd	= 1000 * 'kg/m^3';
end

Model null2ph as str2ph
	EQUATIONS
	Qf 		= 0*'m^3/d';
	Qg 		= 0*'m^3/d';
	T 		= 300 * 'K';
	hO 		= 0.00 * 'kJ/kg';
	hG 		= 0.00 * 'kJ/kg';
	rhoL 	= 800 * 'kg/m^3';
	rhoG 	= 1.00 * 'kg/m^3';
	Rs 		= 0.00 * 'm^3/m^3';
	Rv 		= 0.00 * 'l/m^3';
	Bo 		= 1.00;
	Bg 		= 1.00;
	d60 		= 0.85;
	d60G		= d60;
	Tbn 		= 500*'K';
	TbnG		= Tbn;
	dg 		= 1.00;
	dgL		= dg;
	Ycg 		= 1*'l/m^3';
	YcgL		= Ycg;
end

Model biphase_stream as str2ph
	PARAMETERS
	R			as positive		(Brief="Ideal gas constant",Unit='kJ/kmol/K',Default=8.31447);
	Tref			as temperature		(Brief="Reference temperature for std. cond.",Default=288.70556);
	Pref			as pressure		(Brief="Reference pressure for std. cond.",DisplayUnit='kPa',Default=1);
	rho_H2Ostd	as dens_mass		(Brief="Pure water mass density at std. cond.",Default=999.0054);
	rho_AIRstd	as dens_mass		(Brief="Air mass density at std. cond.",Default=1.2938);

	VARIABLES
	hGres		as enth_mass		(Brief="Gas specific residual enthalpy",Default=-60);
	DhLG			as enth_mass		(Brief="Gas liberation specific enthapy",Default=450);
	Zg			as positive		(Brief="Gas compressibility factor",Default=0.7);
	Tpc			as temperature		(Brief="Gas pseudo-critical temperature",Default=350);
	Ppc			as pressure		(Brief="Gas pseudo-critical pressure",DisplayUnit='kPa',Default=30);

	EQUATIONS
	"Oil specific enthalpy"
	hO/'Btu/lb' = ((T/'degR')-(Tref/'degR'))*(
			(-1.17126+(0.023722+0.024907*d60)*((Tbn/'degR')^(1/3)/d60) 
				+ (1.14982-0.046535*((Tbn/'degR')^(1/3)/d60))/d60)
			+ (1/2)*1e-4*(1+0.82463*((Tbn/'degR')^(1/3)/d60))*(1.12172-0.27634/d60)*
				(707.67^2-(Tref/'degR')^2)/(707.67-(Tref/'degR'))
			- (1/3)*1e-8*(1+0.82463*((Tbn/'degR')^(1/3)/d60))*(2.9027-0.70958/d60)*
				(707.67^3-(Tref/'degR')^3)/(707.67-(Tref/'degR'))
		);

	"Gas specific enthalpy"
	hG - hGres - DhLG = R/(dg*29*'kg/kmol')*(T-Tref)*(
			(0.7457*dg^2 - 1.3405*dg + 2.222) +
			1e-3*(19.894*dg - 1.902)/2*(473.15^2-(Tref/'K')^2)/(473.15-(Tref/'K')) +
			1e-6*(-6.6562*dg + 1.5103)/3*(473.15^3-(Tref/'K')^3)/(473.15-(Tref/'K'))
		);

	"Gas liberation specific enthapy"
	DhLG = R/(dg*29*'kg/kmol')*'degR'*(141.5/d60G-131.5)*(25.724/1.0937);

	"Gas specific residual enthalpy"
	hGres = R/(dg*29*'kg/kmol')*Tpc*(
		(P/Ppc)*((0.083-1.097/(T/Tpc)^1.6) + (0.1432*dg - 0.0669)*(0.139-0.894/(T/Tpc)^4.2)) -
		(Pref/Ppc)*((0.083-1.097/(Tref/Tpc)^1.6) + (0.1432*dg - 0.0669)*(0.139-0.894/(Tref/Tpc)^4.2))
		);

	"Liquid phase mass density"
	rhoL*Bo = d60*rho_H2Ostd;

	"Gas phase mass density"
	rhoG * Zg * R * T = P * (dg*29.0*'kg/kmol');

	"Oil phase formation volume factor"
	Bo = 1 + 4.677e-4*(Rs/'ft^3/bbl') 
		+ 1.751e-5*((T/'degR')-(459.67+60))*(141.5/d60-131.5)/
			(dgL*(1+5.912e-5*(141.5/d60-131.5)*(40*1.8+32)*ln(14.7/114.7)))
		- 1.811e-8*(Rs/'ft^3/bbl')*((T/'degR')-(459.67+60))*(141.5/d60-131.5)/
			(dgL*(1+5.912e-5*(141.5/d60-131.5)*(40*1.8+32)*ln(14.7/114.7)));

	"Gas phase formation volume factor"
	Bg = (Pref/P)*(T/Tref)*Zg;

	"Gas Compressibility Factor"
	Zg = 1 + (0.3265 - 1.0700/(T/Tpc) - 0.5339/(T/Tpc)^3 + 0.01569/(T/Tpc)^4 
		- 0.05165/(T/Tpc)^5)*(0.27*(P/Ppc)/(Zg*(T/Tpc)))
		+ (0.5474 - 0.7361/(T/Tpc) + 0.1844/(T/Tpc)^2)*(0.27*(P/Ppc)/(Zg*(T/Tpc)))^2 
		- 0.1056*(-0.7361/(T/Tpc) + 0.1844/(T/Tpc)^2)*(0.27*(P/Ppc)/(Zg*(T/Tpc)))^5 
		+ 0.6134*exp(-0.7210*((0.27*(P/Ppc)/(Zg*(T/Tpc))))^2)*
			(1 + 0.7210*(0.27*(P/Ppc)/(Zg*(T/Tpc)))^2)*((0.27*(P/Ppc)/(Zg*(T/Tpc)))^2/(T/Tpc)^3);

	"Gas Pseudo-critical temperature and pressure"
	Tpc/'K' = 93.3 + 180.6*dg - 6.94*dg^2;
	Ppc/'bar' = 46.66 + 1.03*dg - 2.58*dg^2;

	"Hypothetical oil specific gravity at gas phase"
	d60G/d60 = (Pref/P)^0.010*exp(100/450 - 100/(T/'K'));

	"Hypothetical oil normal boiling point at gas phase"
	((TbnG/'degR')^(1/3)/d60G) = ((Tbn/'degR')^(1/3)/d60);

	"Dissolved gas specific gravity"
	dg/dgL = (Pref/P)^0.005*exp(600/450 - 600/(T/'K'));

	"Dissolved condensate/gas yield"
	Ycg/YcgL = (Pref/P)^0.005*exp(600/450 - 600/(T/'K'));
end

Model biphase_source
	PARAMETERS
	R			as positive		(Brief="Ideal gas constant",Unit='kJ/kmol/K',Default=8.31447);
	Tref			as temperature		(Brief="Reference temperature for std. cond.",Default=288.70556);
	Pref			as pressure		(Brief="Reference pressure for std. cond.",DisplayUnit='kPa',Default=1);
	rho_H2Ostd	as dens_mass		(Brief="Pure water mass density at std. cond.",Default=999.054);
	rho_AIRstd	as dens_mass		(Brief="Air mass density at std. cond.",Default=1.2938);
	
	VARIABLES
out	Outlet		as biphase_stream;

	Qo			as positive		(Brief="Oil std. vol. flow rate",Unit='m^3/d',Default=1e4);
	Rgo			as positive		(Brief="Gas/oil ratio",Lower=-1e-4,Unit='m^3/m^3',Default=200);
	API			as positive		(Brief="Oil API",Default=30);
	MMg			as molweight		(Brief="Gas molar mass",Default=20);
	Kw			as positive		(Brief="Oil watson K-factor",Default=11.3);
	rhoO			as dens_mass		(Brief="Oil phase mass density at process conditions",Default=800);

	EQUATIONS
	"Oil vol. flow rate at std. cond."
	Qo = Outlet.Qf;
	
	"Gas/oil ratio"
	Outlet.Qg = Rgo * Qo;

	"Oil API grade"
	API = 141.5/Outlet.d60 - 131.5;

	"Gas molar mass"
	MMg = Outlet.dg * 29*'kg/kmol';

	"Oil watson K-factor"
	Kw * Outlet.d60 = (Outlet.Tbn/'degR')^(1/3);

	"Relationship between oil formation volume factor and mass denisty"
	rhoO * Outlet.Bo = Outlet.d60 * rho_H2Ostd;

	"Solution gas/oil ratio"
	Outlet.Rs/'ft^3/bbl' = 
		0.0362*(Outlet.dg*(1 + 5.912e-5*(141.5/Outlet.d60-131.5)*(40*1.8+32)*ln(14.7/114.7)))
		*(Outlet.P/'psi')^1.0937*exp(25.724*(141.5/Outlet.d60-131.5)/(Outlet.T/'degR'));

	"Vaporized oil/gas ratio"
	Outlet.Rv/Outlet.Ycg = (4.7e-7*(Outlet.P/'psi')^2 - 1.4e-3*(Outlet.P/'psi') + 2.3)*
		exp(-5.438e-3*Outlet.dg)*exp(-6.8e8*Outlet.d60/(Outlet.T/'degR')^3);
end

Model biphase_sink
	PARAMETERS
	R			as positive		(Brief="Ideal gas constant",Unit='kJ/kmol/K',Default=8.31447);
	Tref			as temperature		(Brief="Reference temperature for std. cond.",Default=288.70556);
	Pref			as pressure		(Brief="Reference pressure for std. cond.",DisplayUnit='kPa',Default=1);
	rho_H2Ostd	as dens_mass		(Brief="Pure water mass density at std. cond.",Default=999.054);
	rho_AIRstd	as dens_mass		(Brief="Air mass density at std. cond.",Default=1.2938);

	VARIABLES
in	Inlet		as str2ph;

	Qo			as positive		(Brief="Water std. vol. flow rate",Unit='m^3/d',Default=1e4);
	Rgo			as positive		(Brief="Gas/oil ratio",Lower=-1e-4,Unit='m^3/m^3',Default=200);
	API			as positive		(Brief="Oil API",Default=30);
	MMg			as molweight		(Brief="Gas molar mass",Default=20);
	Kw			as positive		(Brief="Oil watson K-factor",Default=11.3);
	rhoO			as dens_mass		(Brief="Oil phase mass density at process conditions",Default=800);

	EQUATIONS
	"Oil vol. flow rate at std. cond."
	Qo = Inlet.Qf;

	"Gas/oil ratio"
	Inlet.Qg = Rgo * Qo;

	"Oil API grade"
	API = 141.5/Inlet.d60 - 131.5;

	"Gas molar mass"
	MMg = Inlet.dg * 29*'kg/kmol';

	"Oil watson K-factor"
	Kw * Inlet.d60 = (Inlet.Tbn/'degR')^(1/3);

	"Relationship between oil formation volume factor and mass density"
	rhoO * Inlet.Bo = Inlet.d60 * rho_H2Ostd;
end

Model biphase_ref
	VARIABLES
in	Inlet	as str2ph;
out	Outlet	as str2ph;

	EQUATIONS
	Outlet.P		= Inlet.P;
	Outlet.T		= Inlet.T;
	Outlet.hO		= Inlet.hO;
	Outlet.hG		= Inlet.hG;
	Outlet.rhoL	= Inlet.rhoL;
	Outlet.rhoG	= Inlet.rhoG;
	Outlet.Rs		= Inlet.Rs;
	Outlet.Rv		= Inlet.Rv;
	Outlet.Bo		= Inlet.Bo;
	Outlet.Bg		= Inlet.Bg;
	Outlet.d60	= Inlet.d60;
	Outlet.d60G	= Inlet.d60G;
	Outlet.Tbn	= Inlet.Tbn;
	Outlet.TbnG	= Inlet.TbnG;
	Outlet.dg		= Inlet.dg;
	Outlet.dgL	= Inlet.dgL;
	Outlet.Ycg	= Inlet.Ycg;
	Outlet.YcgL	= Inlet.YcgL;
end

Model strL as str3ph
	EQUATIONS
	"No gas"
	Qg 		= 0.00 * 'm^3/d';
	hG 		= 0.00 * 'kJ/kg';
	rhoG 	= 1.00 * 'kg/m^3'; 
	Rv 		= 0.00 *  'l/m^3';
	Bg 		= 1.00 * 'm^3/m^3';
	d60G		= d60;
	TbnG		= Tbn;
	dgL		= dg;
	YcgL		= Ycg;
end

Model liquid_stream as strL
	PARAMETERS
	R			as positive		(Brief="Ideal gas constant",Unit='kJ/kmol/K',Default=8.31447);
	Tref			as temperature		(Brief="Reference temperature for std. cond.",Default=288.70556);
	rho_H2Ostd	as dens_mass		(Brief="Pure water mass density at std. cond.",Default=999.0054);

	VARIABLES
	DVWT			as Real			(Brief="Water phase specific volume temperature effect",Default=0);
	DVWP			as Real			(Brief="Water phase specific volume pressure effect",Default=0);
	IDVWT		as Real			(Brief="Integral term of DVWT",Default=30);
	IDVWTSQ		as Real			(Brief="Integral of the DVWT square",Default=150);

	EQUATIONS
	"Oil specific enthalpy"
	hO/'Btu/lb' = ((T/'degR')-(Tref/'degR'))*(
			(-1.17126+(0.023722+0.024907*d60)*((Tbn/'degR')^(1/3)/d60) 
				+ (1.14982-0.046535*((Tbn/'degR')^(1/3)/d60))/d60)
			+ (1/2)*1e-4*(1+0.82463*((Tbn/'degR')^(1/3)/d60))*(1.12172-0.27634/d60)*
				(707.67^2-(Tref/'degR')^2)/(707.67-(Tref/'degR'))
			- (1/3)*1e-8*(1+0.82463*((Tbn/'degR')^(1/3)/d60))*(2.9027-0.70958/d60)*
				(707.67^3-(Tref/'degR')^3)/(707.67-(Tref/'degR'))
		);

	"Water specific enthalpy"
	hW/'kJ/kg'*'K/degR' = ((T-Tref)/'degR')*(
		(4.18 - ws*(rho_Wstd/'kg/m^3')/(1+DVWP)*(4.396e-3 - 4.8e-6*ws*(rho_Wstd/'kg/m^3')/(1+DVWP)))
		- ws*(rho_Wstd/'kg/m^3')/(1+DVWP)*(4.396e-3 - 2*4.8e-6*ws*(rho_Wstd/'kg/m^3')/(1+DVWP))*IDVWT
		+ 4.8e-6*(ws*(rho_Wstd/'kg/m^3')/(1+DVWP))^2*IDVWTSQ
		);

	"Liquid phase mass density"
	rhoL*((1-BSW)*Bo + BSW*Bw) = (1-BSW)*d60*rho_H2Ostd + BSW*rho_Wstd;

	"Oil phase formation volume factor"
	Bo = 1 + 4.677e-4*(Rs/'ft^3/bbl') 
		+ 1.751e-5*((T/'degR')-(459.67+60))*(141.5/d60-131.5)/
			(dg*(1+5.912e-5*(141.5/d60-131.5)*(40*1.8+32)*ln(14.7/114.7)))
		- 1.811e-8*(Rs/'ft^3/bbl')*((T/'degR')-(459.67+60))*(141.5/d60-131.5)/
			(dg*(1+5.912e-5*(141.5/d60-131.5)*(40*1.8+32)*ln(14.7/114.7)));

	"Water phase formation volume factor"
	Bw = (1+DVWP)/(1+DVWT);

	"Water phase (brine) mass density at std. cond."
	rho_Wstd/'lb/ft^3' = 62.638 + 0.438603*ws + 1.60074e-3*ws^2;

	"Water (brine) phase specific volume temperature effect"
	DVWT = -1.0001e-2 + 1.33391e-4*(T/'degR'-459.67) + 5.50654e-7*(T/'degR'-459.67)^2;

	"Water (brine) phase specific volume pressure effect"
	DVWP = -(3.58922e-7 + 1.95301e-9*(T/'degR'-459.67))*(P/'psi')
		-(2.25341e-10 + 1.72834e-13*(T/'degR'-459.67))*(P/'psi')^2;

	"Integral term of water phase specific volume temperature effect"
	IDVWT = -1.0001e-2 + 1.33391e-4/2*(707.67^2-(Tref/'degR')^2)/(707.67-(Tref/'degR'))
		+ 5.50654e-7/3*(707.67^3-(Tref/'degR')^3)/(707.67-(Tref/'degR'));
	
	"Integral of IDVWT square"
	IDVWTSQ = 1.0001^2e-4 - 1.33391e-6*(707.67^2-(Tref/'degR')^2)/(707.67-(Tref/'degR'))
		+ 2.25966e-9*(707.67^3-(Tref/'degR')^3)/(707.67-(Tref/'degR'))
		+ 3.67261e-11*(707.67^4-(Tref/'degR')^4)/(707.67-(Tref/'degR'))
		+ 6.06440e-14*(707.67^5-(Tref/'degR')^5)/(707.67-(Tref/'degR'));
end

Model condensate_stream as strL
	PARAMETERS
	R			as positive		(Brief="Ideal gas constant",Unit='kJ/kmol/K',Default=8.31447);
	Tref			as temperature		(Brief="Reference temperature for std. cond.",Default=288.70556);
	rho_H2Ostd	as dens_mass		(Brief="Pure water mass density at std. cond.",Default=999.0054);

	EQUATIONS
	"Oil specific enthalpy"
	hO/'Btu/lb' = ((T/'degR')-(Tref/'degR'))*(
			(-1.17126+(0.023722+0.024907*d60)*((Tbn/'degR')^(1/3)/d60) 
				+ (1.14982-0.046535*((Tbn/'degR')^(1/3)/d60))/d60)
			+ (1/2)*1e-4*(1+0.82463*((Tbn/'degR')^(1/3)/d60))*(1.12172-0.27634/d60)*
				(707.67^2-(Tref/'degR')^2)/(707.67-(Tref/'degR'))
			- (1/3)*1e-8*(1+0.82463*((Tbn/'degR')^(1/3)/d60))*(2.9027-0.70958/d60)*
				(707.67^3-(Tref/'degR')^3)/(707.67-(Tref/'degR'))
		);

	"Liquid phase mass density"
	rhoL*Bo = d60*rho_H2Ostd;

	"Oil phase formation volume factor"
	Bo = 1 + 4.677e-4*(Rs/'ft^3/bbl') 
		+ 1.751e-5*((T/'degR')-(459.67+60))*(141.5/d60-131.5)/
			(dg*(1+5.912e-5*(141.5/d60-131.5)*(40*1.8+32)*ln(14.7/114.7)))
		- 1.811e-8*(Rs/'ft^3/bbl')*((T/'degR')-(459.67+60))*(141.5/d60-131.5)/
			(dg*(1+5.912e-5*(141.5/d60-131.5)*(40*1.8+32)*ln(14.7/114.7)));
	
	"No Water"
	BSW = 0;
	ws = 0;
	Bw = 1.00;
	rho_Wstd = rho_H2Ostd;
	hW = 0.00 * 'kJ/kg';
end

Model liquid_source
	PARAMETERS
	R			as positive		(Brief="Ideal gas constant",Unit='kJ/kmol/K',Default=8.31447);
	Tref			as temperature		(Brief="Reference temperature for std. cond.",Default=288.70556);
	rho_H2Ostd	as dens_mass		(Brief="Pure water mass density at std. cond.",Default=999.054);
	
	VARIABLES
out	Outlet		as liquid_stream;

	Qo			as positive		(Brief="Oil std. vol. flow rate",Unit='m^3/d',Default=1e4);
	Qw			as positive		(Brief="Water std. vol. flow rate",Unit='m^3/d',Default=1e4);
	API			as positive		(Brief="Oil API",Default=30);
	Kw			as positive		(Brief="Oil watson K-factor",Default=11.3);
	Cws			as positive		(Brief="Water phase salt mass concentration",Unit='g/l',Default=200);
	rhoO			as dens_mass		(Brief="Oil phase mass density at process conditions",Default=800);
	rhoW			as dens_mass		(Brief="Water phase mass density at process conditions",Default=1030);

	EQUATIONS
	"Oil vol. flow rate at std. cond."
	Qo = Outlet.Qf*(1-Outlet.BSW);
	
	"Water vol. flow rate at std. cond."
	Qw = Outlet.Qf*Outlet.BSW;
	
	"Oil API grade"
	API = 141.5/Outlet.d60 - 131.5;

	"Oil watson K-factor"
	Kw * Outlet.d60 = (Outlet.Tbn/'degR')^(1/3);
	
	"Water phase (brine) salt mass concentration"
	Cws*(1-Outlet.ws) = rho_H2Ostd*Outlet.ws;

	"Relationship between oil formation volume factor mass denisty"
	rhoO * Outlet.Bo = Outlet.d60 * rho_H2Ostd;

	"Water phase (brine) mass density @T,P"
	rhoW * Outlet.Bw = Outlet.rho_Wstd;

	"Solution gas/oil ratio"
	Outlet.Rs/'ft^3/bbl' = 
		0.0362*(Outlet.dg*(1 + 5.912e-5*(141.5/Outlet.d60-131.5)*(40*1.8+32)*ln(14.7/114.7)))
		*(Outlet.P/'psi')^1.0937*exp(25.724*(141.5/Outlet.d60-131.5)/(Outlet.T/'degR'));
end

Model condensate_source
	PARAMETERS
	R			as positive		(Brief="Ideal gas constant",Unit='kJ/kmol/K',Default=8.31447);
	Tref			as temperature		(Brief="Reference temperature for std. cond.",Default=288.70556);
	rho_H2Ostd	as dens_mass		(Brief="Pure water mass density at std. cond.",Default=999.054);
	
	VARIABLES
out	Outlet		as condensate_stream;

	Qo			as positive		(Brief="Oil std. vol. flow rate",Unit='m^3/d',Default=1e4);
	API			as positive		(Brief="Oil API",Default=30);
	Kw			as positive		(Brief="Oil watson K-factor",Default=11.3);
	rhoO			as dens_mass		(Brief="Oil phase mass density at process conditions",Default=800);

	EQUATIONS
	"Oil vol. flow rate at std. cond."
	Qo = Outlet.Qf;
	
	"Oil API grade"
	API = 141.5/Outlet.d60 - 131.5;

	"Oil watson K-factor"
	Kw * Outlet.d60 = (Outlet.Tbn/'degR')^(1/3);
	
	"Relationship between oil formation volume factor and mass density"
	rhoO * Outlet.Bo = Outlet.d60 * rho_H2Ostd;

	"Solution gas/oil ratio"
	Outlet.Rs/'ft^3/bbl' = 
		0.0362*(Outlet.dg*(1 + 5.912e-5*(141.5/Outlet.d60-131.5)*(40*1.8+32)*ln(14.7/114.7)))
		*(Outlet.P/'psi')^1.0937*exp(25.724*(141.5/Outlet.d60-131.5)/(Outlet.T/'degR'));
end

Model liquid_sink
	PARAMETERS
	R			as positive		(Brief="Ideal gas constant",Unit='kJ/kmol/K',Default=8.31447);
	Tref			as temperature		(Brief="Reference temperature for std. cond.",Default=288.70556);
	rho_H2Ostd	as dens_mass		(Brief="Pure water mass density at std. cond.",Default=999.054);

	VARIABLES
in	Inlet		as strL;

	Qo			as positive		(Brief="Oil std. vol. flow rate",Unit='m^3/d',Default=1e4);
	Qw			as positive		(Brief="Water std. vol. flow rate",Unit='m^3/d',Default=1e4);
	API			as positive		(Brief="Oil API",Default=30);
	Kw			as positive		(Brief="Oil watson K-factor",Default=11.3);
	Cws			as positive		(Brief="Water phase salt mass concentration",Unit='g/l',Default=200);
	rhoO			as dens_mass		(Brief="Oil phase mass density at process conditions",Default=800);
	rhoW			as dens_mass		(Brief="Water phase mass density at process conditions",Default=1030);

	EQUATIONS
	"Oil vol. flow rate at std. cond."
	Qo = Inlet.Qf*(1-Inlet.BSW);
	
	"Water vol. flow rate at std. cond."
	Qw = Inlet.Qf*Inlet.BSW;
	
	"Oil API grade"
	API = 141.5/Inlet.d60 - 131.5;

	"Oil watson K-factor"
	Kw * Inlet.d60 = (Inlet.Tbn/'degR')^(1/3);
	
	"Water phase (brine) salt mass concentration"
	Cws*(1-Inlet.ws) = rho_H2Ostd*Inlet.ws;
	
	"Relationship between oil formation volume factor and mass density"
	rhoO * Inlet.Bo = Inlet.d60 * rho_H2Ostd;

	"Water phase (brine) mass density @T,P"
	rhoW * Inlet.Bw = Inlet.rho_Wstd;
end

Model condensate_sink as liquid_sink

end

Model strGL
	VARIABLES
	Qg		as positive		(Brief="Gas std. vol. flow rate",Unit='m^3/d',Default=1e6);
	Qo		as positive		(Brief="Oil std. vol. flow rate",Unit='m^3/d',Default=1e4);
	P       as pressure    	(Brief="Pressure",DisplayUnit='kPa',Default=10);
	T       as temperature		(Brief="Temperature",Default=300);
	hO		as enth_mass		(Brief="Oil specific enthalpy",Default=250);
	hG		as enth_mass		(Brief="Gas specific enthalpy",Default=400);
	rhoL		as dens_mass		(Brief="Liquid phase mass density",Default=900);
	rhoG		as dens_mass		(Brief="Gas phase mass density",Default=10);
	Rs		as positive		(Brief="Solution gas-oil ratio",Unit='m^3/m^3',Default=10);
	Rv		as positive		(Brief="Vaporized oil-gas ratio",Unit='l/m^3',Default=0.01);
	Bo		as positive		(Brief="Oil phase FVF",Unit='m^3/m^3',Default=1.05);
	Bg		as positive		(Brief="Gas phase FVF",Unit='m^3/m^3',Default=0.1);
	d60		as positive		(Brief="Oil specific gravity at 60 °F",Lower=1e-6,Default=0.85);
	d60G		as positive		(Brief="Hypothetical d60 for gas phase",Lower=1e-6,Default=0.6);
	Tbn		as temperature		(Brief="Oil normal boiling point",Default=450);
	TbnG		as temperature		(Brief="Hypothetial Tbn for gas phase",Default=250);
	dg		as positive		(Brief="Gas specific gravity",Default=0.7);
	dgL		as positive		(Brief="Solution gas specific gravity",Default=0.95);
	Ycg		as positive		(Brief="Condensate/gas yield",Unit='l/m^3',Default=4);
	YcgL		as positive		(Brief="Solution condensate/gas yield",Unit='l/m^3',Default=5.5);
end

Model nullGL as strGL
	EQUATIONS
	Qg 		= 0*'m^3/d';
	Qo 		= 0*'m^3/d';
	T 		= 300 * 'K';
	hO 		= 0.00 * 'kJ/kg';
	hG 		= 0.00 * 'kJ/kg';
	rhoL 	= 800 * 'kg/m^3';
	rhoG 	= 1.00 * 'kg/m^3';
	Rs 		= 0.00 * 'm^3/m^3';
	Rv 		= 0.00 * 'l/m^3';
	Bo 		= 1.00;
	Bg 		= 1.00;
	d60 		= 0.85;
	d60G		= d60;
	Tbn 		= 500*'K';
	TbnG		= Tbn;
	dg 		= 1.00;
	dgL		= dg;
	Ycg 		= 1*'l/m^3';
	YcgL		= Ycg;
end

Model gasliq_stream as strGL
	PARAMETERS
	R			as positive		(Brief="Ideal gas constant",Unit='kJ/kmol/K',Default=8.31447);
	Tref			as temperature		(Brief="Reference temperature for std. cond.",Default=288.70556);
	Pref			as pressure		(Brief="Reference pressure for std. cond.",DisplayUnit='kPa',Default=1);
	rho_H2Ostd	as dens_mass		(Brief="Pure water mass density at std. cond.",Default=999.0054);
	rho_AIRstd	as dens_mass		(Brief="Air mass density at std. cond.",Default=1.2938);

	VARIABLES
	hGres		as enth_mass		(Brief="Gas specific residual enthalpy",Default=-60);
	DhLG			as enth_mass		(Brief="Gas liberation specific enthapy",Default=450);
	Zg			as positive		(Brief="Gas compressibility factor",Default=0.7);
	Tpc			as temperature		(Brief="Gas pseudo-critical temperature",Default=350);
	Ppc			as pressure		(Brief="Gas pseudo-critical pressure",DisplayUnit='kPa',Default=30);
	
	EQUATIONS
	"Oil specific enthalpy"
	hO/'Btu/lb' = ((T/'degR')-(Tref/'degR'))*(
			(-1.17126+(0.023722+0.024907*d60)*((Tbn/'degR')^(1/3)/d60) 
				+ (1.14982-0.046535*((Tbn/'degR')^(1/3)/d60))/d60)
			+ (1/2)*1e-4*(1+0.82463*((Tbn/'degR')^(1/3)/d60))*(1.12172-0.27634/d60)*
				(707.67^2-(Tref/'degR')^2)/(707.67-(Tref/'degR'))
			- (1/3)*1e-8*(1+0.82463*((Tbn/'degR')^(1/3)/d60))*(2.9027-0.70958/d60)*
				(707.67^3-(Tref/'degR')^3)/(707.67-(Tref/'degR'))
		);

	"Gas specific enthalpy"
	hG - hGres - DhLG = R/(dg*29*'kg/kmol')*(T-Tref)*(
			(0.7457*dg^2 - 1.3405*dg + 2.222) +
			1e-3*(19.894*dg - 1.902)/2*(473.15^2-(Tref/'K')^2)/(473.15-(Tref/'K')) +
			1e-6*(-6.6562*dg + 1.5103)/3*(473.15^3-(Tref/'K')^3)/(473.15-(Tref/'K'))
		);

	"Gas liberation specific enthapy"
	DhLG = R/(dg*29*'kg/kmol')*'degR'*(141.5/d60G-131.5)*(25.724/1.0937);

	"Gas specific residual enthalpy"
	hGres = R/(dg*29*'kg/kmol')*Tpc*(
		(P/Ppc)*((0.083-1.097/(T/Tpc)^1.6) + (0.1432*dg - 0.0669)*(0.139-0.894/(T/Tpc)^4.2)) -
		(Pref/Ppc)*((0.083-1.097/(Tref/Tpc)^1.6) + (0.1432*dg - 0.0669)*(0.139-0.894/(Tref/Tpc)^4.2))
		);

	"Liquid phase mass density"
	rhoL * Bo = d60 * rho_H2Ostd;

	"Gas phase mass density"
	rhoG * Zg * R * T = P * (dg*29.0*'kg/kmol');

	"Oil phase formation volume factor"
	Bo = 1 + 4.677e-4*(Rs/'ft^3/bbl') 
		+ 1.751e-5*((T/'degR')-(459.67+60))*(141.5/d60-131.5)/
			(dgL*(1+5.912e-5*(141.5/d60-131.5)*(40*1.8+32)*ln(14.7/114.7)))
		- 1.811e-8*(Rs/'ft^3/bbl')*((T/'degR')-(459.67+60))*(141.5/d60-131.5)/
			(dgL*(1+5.912e-5*(141.5/d60-131.5)*(40*1.8+32)*ln(14.7/114.7)));

	"Gas phase formation volume factor"
	Bg = (Pref/P)*(T/Tref)*Zg;

	"Gas compressibility factor"
	Zg = 1 + (0.3265 - 1.0700/(T/Tpc) - 0.5339/(T/Tpc)^3 + 0.01569/(T/Tpc)^4 
		- 0.05165/(T/Tpc)^5)*(0.27*(P/Ppc)/(Zg*(T/Tpc)))
		+ (0.5474 - 0.7361/(T/Tpc) + 0.1844/(T/Tpc)^2)*(0.27*(P/Ppc)/(Zg*(T/Tpc)))^2 
		- 0.1056*(-0.7361/(T/Tpc) + 0.1844/(T/Tpc)^2)*(0.27*(P/Ppc)/(Zg*(T/Tpc)))^5 
		+ 0.6134*exp(-0.7210*((0.27*(P/Ppc)/(Zg*(T/Tpc))))^2)*
			(1 + 0.7210*(0.27*(P/Ppc)/(Zg*(T/Tpc)))^2)*((0.27*(P/Ppc)/(Zg*(T/Tpc)))^2/(T/Tpc)^3);

	"Gas pseudo-critical temperature and pressure"
	Tpc/'K' = 93.3 + 180.6*dg - 6.94*dg^2;
	Ppc/'bar' = 46.66 + 1.03*dg - 2.58*dg^2;

	"Condensed oil specific gravity"
	d60G/d60 = (Pref/P)^0.010*exp(100/450 - 100/(T/'K'));

	"Condensed oil normal boiling point"
	((TbnG/'degR')^(1/3)/d60G) = ((Tbn/'degR')^(1/3)/d60);

	"Dissolved gas specific gravity"
	dg/dgL = (Pref/P)^0.005*exp(600/450 - 600/(T/'K'));

	"Dissolved condensate/gas yield"
	Ycg/YcgL = (Pref/P)^0.005*exp(600/450 - 600/(T/'K'));
end

Model gasliq_source
	PARAMETERS
	R			as positive		(Brief="Ideal gas constant",Unit='kJ/kmol/K',Default=8.31447);
	Tref			as temperature		(Brief="Reference temperature for std. cond.",Default=288.70556);
	Pref			as pressure		(Brief="Reference pressure for std. cond.",DisplayUnit='kPa',Default=1);
	rho_H2Ostd	as dens_mass		(Brief="Pure water mass density at std. cond.",Default=999.054);
	rho_AIRstd	as dens_mass		(Brief="Air mass density at std. cond.",Default=1.2938);
	
	VARIABLES
out	Outlet		as gasliq_stream;
	
	Qg_act		as positive		(Brief="Gas vol. flow rate at act. cond.",Unit='m^3/h',Default=1e4);
	Rcg			as positive		(Brief="Condensate/gas ratio",Unit='l/m^3',Default=0.005);
	API			as positive		(Brief="Oil API",Default=30);
	MMg			as molweight		(Brief="Gas molar mass",Default=20);
	Kw			as positive		(Brief="Oil watson K-factor",Default=11.3);
	
	EQUATIONS
	"Volumetric flow rate @T,P"
	Qg_act = Outlet.Bg*Outlet.Qg;

	"Oil vol. flow rate at std. cond."
	Outlet.Qo = Rcg * Outlet.Qg;

	"Oil API grade"
	API = 141.5/Outlet.d60 - 131.5;

	"Gas molar mass"
	MMg = Outlet.dg * 29*'kg/kmol';

	"Oil watson K-factor"
	Kw * Outlet.d60 = (Outlet.Tbn/'degR')^(1/3);

	"Solution gas/oil ratio"
	Outlet.Rs/'ft^3/bbl' = 
		0.0362*(Outlet.dg*(1 + 5.912e-5*(141.5/Outlet.d60-131.5)*(40*1.8+32)*ln(14.7/114.7)))
		*(Outlet.P/'psi')^1.0937*exp(25.724*(141.5/Outlet.d60-131.5)/(Outlet.T/'degR'));

	"Vaporized oil/gas ratio"
	Outlet.Rv/Outlet.Ycg = (4.7e-7*(Outlet.P/'psi')^2 - 1.4e-3*(Outlet.P/'psi') + 2.3)*
		exp(-5.438e-3*Outlet.dg)*exp(-6.8e8*Outlet.d60/(Outlet.T/'degR')^3);
end

Model gasliq_sink
	PARAMETERS
	R			as positive		(Brief="Ideal gas constant",Unit='kJ/kmol/K',Default=8.31447);
	Tref			as temperature		(Brief="Reference temperature for std. cond.",Default=288.70556);
	Pref			as pressure		(Brief="Reference pressure for std. cond.",DisplayUnit='kPa',Default=1);
	rho_H2Ostd	as dens_mass		(Brief="Pure water mass density at std. cond.",Default=999.054);

	VARIABLES
in	Inlet		as strGL;
	
	Qg_act		as positive		(Brief="Gas vol. flow rate at act. cond.",Unit='m^3/h',Default=1e4);
	Rcg			as positive		(Brief="Condensate/gas ratio",Unit='l/m^3',Default=0.005);
	API			as positive		(Brief="Oil API",Default=30);
	MMg			as molweight		(Brief="Gas molar mass",Default=20);
	Kw			as positive		(Brief="Oil watson K-factor",Default=11.3);
	
	EQUATIONS
	"Volumetric flow rate @T,P"
	Qg_act = Inlet.Bg * Inlet.Qg;

	"Oil vol. flow rate at std. cond."
	Inlet.Qo = Rcg * Inlet.Qg;

	"Oil API grade"
	API = 141.5/Inlet.d60 - 131.5;

	"Gas molar mass"
	MMg = Inlet.dg * 29*'kg/kmol';

	"Oil watson K-factor"
	Kw * Inlet.d60 = (Inlet.Tbn/'degR')^(1/3);
end

Model gasliq_ref
	VARIABLES
in	Inlet	as strGL;
out	Outlet	as strGL;

	EQUATIONS
	Outlet.P		= Inlet.P;
	Outlet.T		= Inlet.T;
	Outlet.hO		= Inlet.hO;
	Outlet.hG		= Inlet.hG;
	Outlet.rhoL	= Inlet.rhoL;
	Outlet.rhoG	= Inlet.rhoG;
	Outlet.Rs		= Inlet.Rs;
	Outlet.Rv		= Inlet.Rv;
	Outlet.Bo		= Inlet.Bo;
	Outlet.Bg		= Inlet.Bg;
	Outlet.d60	= Inlet.d60;
	Outlet.d60G	= Inlet.d60G;
	Outlet.Tbn	= Inlet.Tbn;
	Outlet.TbnG	= Inlet.TbnG;
	Outlet.dg		= Inlet.dg;
	Outlet.dgL	= Inlet.dgL;
	Outlet.Ycg	= Inlet.Ycg;
	Outlet.YcgL	= Inlet.YcgL;
end

Model strG as strGL
	EQUATIONS
	Qo 		= 0.00 * 'm^3/d';
	hO 		= 0.00 * 'kJ/kg';
	rhoL 	= 1000 * 'kg/m^3';
	Rs 		= 0.00 * 'm^3/m^3';
	Bo		= 1.00 * 'm^3/m^3';
	d60G		= d60;
	TbnG		= Tbn;
	dgL		= dg;
	YcgL		= Ycg;
end

Model nullG as strG
	EQUATIONS
	Qg 		= 0*'m^3/d';
	T 		= 300 * 'K';
	hG 		= 0.00 * 'kJ/kg';
	rhoG 	= 1.00 * 'kg/m^3';
	Rv 		= 0.00 * 'l/m^3';
	Bg 		= 1.00;
	d60 		= 0.85;
	Tbn 		= 500*'K';
	dg 		= 1.00;
	Ycg 		= 1*'l/m^3';
end

Model gas_stream as strG
	PARAMETERS
	R			as positive		(Brief="Ideal gas constant",Unit='kJ/kmol/K',Default=8.31447);
	Tref			as temperature		(Brief="Reference temperature for std. cond.",Default=288.70556);
	Pref			as pressure		(Brief="Reference pressure for std. cond.",DisplayUnit='kPa',Default=1);
	rho_AIRstd	as dens_mass		(Brief="Air mass density at std. cond.",Default=1.2938);

	VARIABLES
	hGres		as enth_mass		(Brief="Gas specific residual enthalpy",Default=-60);
	DhLG			as enth_mass		(Brief="Gas liberation specific enthapy",Default=450);
	Zg			as positive		(Brief="Gas compressibility factor",Default=0.7);
	Tpc			as temperature		(Brief="Gas pseudo-critical temperature",Default=350);
	Ppc			as pressure		(Brief="Gas pseudo-critical pressure",DisplayUnit='kPa',Default=30);
		
	EQUATIONS
	"Gas specific enthalpy"
	hG - hGres - DhLG = R/(dg*29*'kg/kmol')*(T-Tref)*(
			(0.7457*dg^2 - 1.3405*dg + 2.222) +
			1e-3*(19.894*dg - 1.902)/2*(473.15^2-(Tref/'K')^2)/(473.15-(Tref/'K')) +
			1e-6*(-6.6562*dg + 1.5103)/3*(473.15^3-(Tref/'K')^3)/(473.15-(Tref/'K'))
		);

	"Gas liberation specific enthapy"
	DhLG = R/(dg*29*'kg/kmol')*'degR'*(141.5/d60G-131.5)*(25.724/1.0937);

	"Gas specific residual enthalpy"
	hGres = R/(dg*29*'kg/kmol')*Tpc*(
		(P/Ppc)*((0.083-1.097/(T/Tpc)^1.6) + (0.1432*dg - 0.0669)*(0.139-0.894/(T/Tpc)^4.2)) -
		(Pref/Ppc)*((0.083-1.097/(Tref/Tpc)^1.6) + (0.1432*dg - 0.0669)*(0.139-0.894/(Tref/Tpc)^4.2))
		);

	"Gas phase mass density"
	rhoG * Zg * R * T = P * (dg*29.0*'kg/kmol');

	"Gas phase formation volume factor"
	Bg = (Pref/P)*(T/Tref)*Zg;

	"Gas compressibility factor"
	Zg = 1 + (0.3265 - 1.0700/(T/Tpc) - 0.5339/(T/Tpc)^3 + 0.01569/(T/Tpc)^4 
		- 0.05165/(T/Tpc)^5)*(0.27*(P/Ppc)/(Zg*(T/Tpc)))
		+ (0.5474 - 0.7361/(T/Tpc) + 0.1844/(T/Tpc)^2)*(0.27*(P/Ppc)/(Zg*(T/Tpc)))^2 
		- 0.1056*(-0.7361/(T/Tpc) + 0.1844/(T/Tpc)^2)*(0.27*(P/Ppc)/(Zg*(T/Tpc)))^5 
		+ 0.6134*exp(-0.7210*((0.27*(P/Ppc)/(Zg*(T/Tpc))))^2)*
			(1 + 0.7210*(0.27*(P/Ppc)/(Zg*(T/Tpc)))^2)*((0.27*(P/Ppc)/(Zg*(T/Tpc)))^2/(T/Tpc)^3);

	"Gas pseudo-critical temperature and pressure"
	Tpc/'K' = 93.3 + 180.6*dg - 6.94*dg^2;
	Ppc/'bar' = 46.66 + 1.03*dg - 2.58*dg^2;
end

Model gas_source
	PARAMETERS
	R			as positive		(Brief="Ideal gas constant",Unit='kJ/kmol/K',Default=8.31447);
	Tref			as temperature		(Brief="Reference temperature for std. cond.",Default=288.70556);
	Pref			as pressure		(Brief="Reference pressure for std. cond.",DisplayUnit='kPa',Default=1);
	rho_AIRstd	as dens_mass		(Brief="Air mass density at std. cond.",Default=1.2938);
	
	VARIABLES
out	Outlet		as gas_stream;
	
	Qg_act		as positive		(Brief="Gas vol. flow rate at act. cond.",Unit='m^3/h',Default=1e4);
	API			as positive		(Brief="Oil API",Default=30);
	MMg			as molweight		(Brief="Gas molar mass",Default=20);
	Kw			as positive		(Brief="Oil watson K-factor",Default=11.3);
	
	EQUATIONS
	"Volumetric flow rate @T,P"
	Qg_act = Outlet.Bg*Outlet.Qg;

	"Oil API grade"
	API = 141.5/Outlet.d60 - 131.5;

	"Gas molar mass"
	MMg = Outlet.dg * 29*'kg/kmol';

	"Oil watson K-factor"
	Kw * Outlet.d60 = (Outlet.Tbn/'degR')^(1/3);

	"Vaporized oil/gas ratio"
	Outlet.Rv/Outlet.Ycg = (4.7e-7*(Outlet.P/'psi')^2 - 1.4e-3*(Outlet.P/'psi') + 2.3)*
		exp(-5.438e-3*Outlet.dg)*exp(-6.8e8*Outlet.d60/(Outlet.T/'degR')^3);
end

Model gas_sink
	PARAMETERS
	R			as positive		(Brief="Ideal gas constant",Unit='kJ/kmol/K',Default=8.31447);
	Tref			as temperature		(Brief="Reference temperature for std. cond.",Default=288.70556);
	Pref			as pressure		(Brief="Reference pressure for std. cond.",DisplayUnit='kPa',Default=1);
	rho_AIRstd	as dens_mass		(Brief="Air mass density at std. cond.",Default=1.2938);

	VARIABLES
in	Inlet		as strG;
	
	Qg_act		as positive		(Brief="Gas vol. flow rate at act. cond.",Unit='m^3/h',Default=1e4);
	API			as positive		(Brief="Oil API",Default=30);
	MMg			as molweight		(Brief="Gas molar mass",Default=20);
	Kw			as positive		(Brief="Oil watson K-factor",Default=11.3);
	
	EQUATIONS
	"Volumetric flow rate @T,P"
	Qg_act = Inlet.Bg*Inlet.Qg;

	"Oil API grade"
	API = 141.5/Inlet.d60 - 131.5;

	"Gas molar mass"
	MMg = Inlet.dg * 29*'kg/kmol';

	"Oil watson K-factor"
	Kw * Inlet.d60 = (Inlet.Tbn/'degR')^(1/3);
end

Model gas_ref
	VARIABLES
in	Inlet	as strG;
out	Outlet	as strG;

	EQUATIONS
	Outlet.P		= Inlet.P;
	Outlet.T		= Inlet.T;
	Outlet.hG		= Inlet.hG;
	Outlet.rhoG	= Inlet.rhoG;
	Outlet.Rv		= Inlet.Rv;
	Outlet.Bg		= Inlet.Bg;
	Outlet.d60G	= Inlet.d60G;
	Outlet.TbnG	= Inlet.TbnG;
	Outlet.dg		= Inlet.dg;
	Outlet.Ycg	= Inlet.Ycg;
end

Model strW
	VARIABLES
	Qw		as positive		(Brief="Water std. vol. flow rate",Unit='m^3/d',Default=1e4);
	P       as pressure    	(Brief="Pressure",DisplayUnit='kPa',Default=10);
	T		as temperature 	(Brief="Temperature",Default=300);	
	ws		as fraction		(Brief="Water phase salt weitgh content",Default=0.2);
	TOG		as positive		(Brief="Grease and oil content",Unit='ml/m^3',Default=100);
	hW		as enth_mass		(Brief="Water phase specific enthalpy",Default=250);
	rhoW		as dens_mass		(Brief="Water phase mass density",Default=1030);
	rho_Wstd	as dens_mass		(Brief="Water phase mass density at std. cond.",Default=1005);
	d60		as positive		(Brief="Emulsified oil specific gravity at 60 °F",Lower=1e-6,Default=0.8);
	Tbn		as temperature		(Brief="Emulsified oil normal boiling point",Default=650);
end

Model water_stream as strW
	PARAMETERS
	Tref		as temperature		(Brief="Reference temperature for std. cond.",Default=288.70556);

	VARIABLES
	Bw		as positive		(Brief="Water phase FVF",Default= 0.95);
	DVWT		as Real			(Brief="Water phase specific volume temperature effect",Default=0);
	DVWP		as Real			(Brief="Water phase specific volume pressure effect",Default=0);
	IDVWT	as Real			(Brief="Integral term of DVWT",Default=30);
	IDVWTSQ	as Real			(Brief="Integral of the DVWT square",Default=150);

	EQUATIONS
	"Water specific enthalpy"
	hW/'kJ/kg'*'K/degR' = ((T-Tref)/'degR')*(
		(4.18 - ws*(rho_Wstd/'kg/m^3')/(1+DVWP)*(4.396e-3 - 4.8e-6*ws*(rho_Wstd/'kg/m^3')/(1+DVWP)))
		- ws*(rho_Wstd/'kg/m^3')/(1+DVWP)*(4.396e-3 - 2*4.8e-6*ws*(rho_Wstd/'kg/m^3')/(1+DVWP))*IDVWT
		+ 4.8e-6*(ws*(rho_Wstd/'kg/m^3')/(1+DVWP))^2*IDVWTSQ
		);

	"Water phase (brine) mass density"
	rhoW = Bw * rho_Wstd;

	"Water phase formation volume factor"
	Bw = (1+DVWP)/(1+DVWT);

	"Water phase (brine) mass density at std. cond."
	rho_Wstd/'lb/ft^3' = 62.638 + 0.438603*ws + 1.60074e-3*ws^2;

	"Water (brine) phase specific volume temperature effect"
	DVWT = -1.0001e-2 + 1.33391e-4*(T/'degR'-459.67) + 5.50654e-7*(T/'degR'-459.67)^2;

	"Water (brine) phase specific volume pressure effect"
	DVWP = -(3.58922e-7 + 1.95301e-9*(T/'degR'-459.67))*(P/'psi')
		-(2.25341e-10 + 1.72834e-13*(T/'degR'-459.67))*(P/'psi')^2;

	"Integral term of water phase specific volume temperature effect"
	IDVWT = -1.0001e-2 + 1.33391e-4/2*(707.67^2-(Tref/'degR')^2)/(707.67-(Tref/'degR'))
		+ 5.50654e-7/3*(707.67^3-(Tref/'degR')^3)/(707.67-(Tref/'degR'));
	
	"Integral of IDVWT square"
	IDVWTSQ = 1.0001^2e-4 - 1.33391e-6*(707.67^2-(Tref/'degR')^2)/(707.67-(Tref/'degR'))
		+ 2.25966e-9*(707.67^3-(Tref/'degR')^3)/(707.67-(Tref/'degR'))
		+ 3.67261e-11*(707.67^4-(Tref/'degR')^4)/(707.67-(Tref/'degR'))
		+ 6.06440e-14*(707.67^5-(Tref/'degR')^5)/(707.67-(Tref/'degR'));
end

Model water_source
	PARAMETERS
	rho_H2Ostd	as dens_mass		(Brief="Pure water mass density at std. cond.",Default=999.054);
	
	VARIABLES
out	Outlet		as water_stream;

	Cws			as positive		(Brief="Water phase salt mass concentration",Unit='g/l',Default=200);

	EQUATIONS	
	"Water phase (brine) salt mass concentration"
	Cws*(1-Outlet.ws) = rho_H2Ostd*Outlet.ws;
end

Model water_sink
	PARAMETERS
	rho_H2Ostd	as dens_mass		(Brief="Pure water mass density at std. cond.",Default=999.054);

	VARIABLES
in	Inlet		as strW;

	Cws			as positive		(Brief="Water phase salt mass concentration",Unit='g/l',Default=200);

	EQUATIONS
	"Water phase (brine) salt mass concentration"
	Cws*(1-Inlet.ws) = rho_H2Ostd*Inlet.ws;
end
